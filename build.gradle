buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://files.minecraftforge.net/maven' }
        maven { url "https://dl.bintray.com/kotlin/kotlin-eap" }
    }
    dependencies {
        classpath(group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true) {
            exclude group: 'trove', module: 'trove'
        }
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.4'
    id 'org.jmailen.kotlinter' version '3.0.2'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'kotlin'
apply plugin: 'eclipse'

group = 'net.alexwells.kottle'
archivesBaseName = 'Kottle'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileKotlin.kotlinOptions.jvmTarget = compileTestKotlin.kotlinOptions.jvmTarget = '1.8'

minecraft {
    mappings channel: mappingsChannel, version: mappingsVersion

    runs {
        client {
            workingDirectory project.file('run')

            // recommended logging data for a userdev environment
            properties 'forge.logging.markers': 'CORE,SCAN,REGISTRIES'

            // recommended logging level for the console
            properties 'forge.logging.console.level': 'info'

            // enable console colors
            properties 'forge.logging.noansi': 'false'

            mods {
                Kottle {
                    source sourceSets.main
                }
            }
        }

        testClient {
            parent runs.client

            mods {
                KottleTest {
                    source sourceSets.test
                }
            }
        }

        server {
            workingDirectory project.file('run')

            // recommended logging data for a userdev environment
            properties 'forge.logging.markers': 'CORE,SCAN,REGISTRIES'

            // recommended logging level for the console
            properties 'forge.logging.console.level': 'debug'

            // enable console colors
            properties 'forge.logging.noansi': 'false'

            mods {
                Kottle {
                    source sourceSets.main
                }
            }
        }
    }
}

jar {
    classifier = 'slim'

    manifest {
        attributes([
                'FMLModType': 'LANGPROVIDER'
        ])

        attributes([
                'Specification-Title'     : 'Mod Language Provider',
                'Specification-Vendor'    : 'Forge Development LLC',
                'Specification-Version'   : '1',
                'Implementation-Title'    : archivesBaseName,
                'Implementation-Version'  : version,
                'Implementation-Vendor'   : 'kottle',
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ], 'net/alexwells/kottle/')
    }
}

repositories {
    jcenter()
    maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    maven { url 'https://kotlin.bintray.com/kotlinx' }
}

configurations {
    compile.extendsFrom(shade)
}

dependencies {
    minecraft forgeVersion

    shade "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    shade "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    shade "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    shade "org.jetbrains:annotations:$annotationsVersion"
    shade "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    shade "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:$coroutinesVersion"
}

shadowJar {
    classifier = ''
    configurations = [project.configurations.shade]
}

tasks.build.dependsOn shadowJar

artifacts {
    archives shadowJar
    archives jar
}
